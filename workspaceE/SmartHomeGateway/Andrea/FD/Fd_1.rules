import java.util.concurrent.locks.ReentrantLock

var boolean gf_mod
var boolean ff_mod
var boolean flag_precedenza_gf
var boolean flag_precedenza_ff
var boolean gf_call
var boolean ff_call
var boolean adaptation_required_gf        // varibili per la richiesta di adattamento per gf e ff
var boolean adaptation_required_ff                                   

val ReentrantLock lock_m  = new ReentrantLock()
val ReentrantLock lock_e = new ReentrantLock()

var Number cbTemp 
var Number bTemp 
var Number kiTemp 
var Number batTemp 
var Number lzOneTemp 
var Number lzTwoTemp 
var Number gf_bat
var Number gf_kit
var Number gf_liv1
var Number gf_liv2
var Number ff_bed
var Number ff_cb

rule "StartLoop_MySmartHomeFD"
when
	System started
then
	start_m_gf.sendCommand(ON)  // Setto le componente a OFF ad eccezione dei monitor
	start_m_ff.sendCommand(ON)
	start_a_fd.sendCommand(OFF)
	start_p_fd.sendCommand(OFF)
	start_e_gf.sendCommand(OFF)
	start_e_ff.sendCommand(OFF)

    gf_liv1= 0        // Inizializzazione variabili per la componente P
	gf_liv2= 0
	gf_bat= 0
	gf_kit= 0
	ff_bed= 0
	ff_cb= 0

	gf_mod= false                   // iniziializzo i flag di "mod" utilizzati per tener traccia di quali monitor li abbia chiamati all'interno delle componenti E)
	ff_mod= false
	gf_call= false                  // inizializzo i flag di "call" e di "precedenza" utilizzati dalla componente A per poter richiamare 
	ff_call= false                 //  l'esatto monitor che ha avviato tale componente
	flag_precedenza_ff= false
	flag_precedenza_gf= false
	adaptation_required_gf= false              // inizializzo le variabili di adattamento a "false"
	adaptation_required_ff= false
	if (Managing_System_Active.state == ON) Managing_System_Active.postUpdate(OFF)
end 

rule "Monitor_m_gf"
when
	Item start_m_gf received command ON
then 
    try {
        lock_m.lock()
    } finally {
        if (!flag_precedenza_ff) {      // nel caso il flag_precedenza_ff sia spento allora questo ciclo ha la precedenza in quanto è arrivato per primo

            flag_precedenza_gf= true       
        }
        gf_call= true
        lock_m.unlock()
    }
    kiTemp = GF_Kitchen_Temperature.state as Number 
	batTemp = GF_Bathroom_Temperature.state as Number
	lzOneTemp = GF_LZone1_Temperature.state as Number
	lzTwoTemp = GF_LZone2_Temperature.state as Number

    sendCommand(start_m_gf, OFF)
	sendCommand(gf_slave_to_fd_master, ON)
end

rule "Monitor_m_ff"
when
	Item start_m_ff received command ON
then
	
    try {
        lock_m.lock()
    } finally {
        if (!flag_precedenza_gf) {      // nel caso il flag_precedenza_ff sia spento allora questo ciclo ha la precedenza in quanto è arrivato per primo

            flag_precedenza_ff= true       
        }
        ff_call= true
        lock_m.unlock()
    }
    cbTemp = FF_Childrenroom_Temperature.state as Number 
	bTemp = FF_Bedroom_Temperature.state as Number

	sendCommand(start_m_ff, OFF)
	sendCommand(ff_slave_to_fd_master, ON) 
end

rule "Aggregator_m_gf_m_ff_a_fd"
when
	Item gf_slave_to_fd_master received command ON or
	Item ff_slave_to_fd_master received command ON 
then
	if (gf_slave_to_fd_master.state== ON) {
		sendCommand(gf_slave_to_fd_master, OFF)
	}

	else if (ff_slave_to_fd_master.state== ON){
		sendCommand(ff_slave_to_fd_master, OFF)
	} 
	
	do {                          // serie di cicli do-while che garantisce la mutua esclusione nella componente A, e nel caso ci 
		do {                      // ci sia un addatamento garantisce mutua esclusione fino alle componenti E cosi da non sovrascrivere
            Thread::sleep(1000)
		} while (gf_mod || ff_mod)
        
        Thread::sleep(1000)
	} while (start_a_fd.state== ON)

	sendCommand (start_a_fd, ON)
end 

rule "Analyze_a_fd"
when
	Item start_a_fd received command ON
then
    if(((lzOneTemp >= 45 || lzTwoTemp >= 45) && GF_Living_Fire_Alarm.state == OFF) ||
        (kiTemp >= 45 && GF_Kitchen_Fire_Alarm.state== OFF) || 
        (batTemp >= 45 && GF_Bathroom_Fire_Alarm.state== OFF)) {

        adaptation_required_gf = true
    } 

    if((cbTemp >= 45 && FF_Children_Bedroom_Fire_Alarm.state== OFF) ||
        (bTemp >= 45 && FF_Bedroom_Fire_Alarm.state== OFF)) {

        adaptation_required_ff = true
    }

	if(adaptation_required_gf || adaptation_required_ff) {   // verifico quale dei 2 cicli ha la precedenza e vado ad abilitate le variabili mod rispettive
		 if (gf_call && flag_precedenza_gf) {    // disabilito i flag di call e di precedenza del piano gf se quest'ultimo ha la precedenza                 
			gf_call= false
			flag_precedenza_gf= false
			if (ff_call && !flag_precedenza_ff)  // abilito il flag precedenza della controparte, nel caso sia attivo un monitor senza procedenza di quest'ultimo
				flag_precedenza_ff= true		// (siccome ci deve essere sempre un monitor con precedenza)

			gf_mod= true      // abilito il flag di gf_mod per tener traccia del fatto che è stato il piano gf a richiedere un adattamento
	 	}
		else if (ff_call && flag_precedenza_ff) {  // disabilito i flag di call e di precedenza del piano ff se quest'ultimo ha la precedenza
			ff_call= false
			flag_precedenza_ff= false
			if (gf_call && !flag_precedenza_gf) // abilito il flag precedenza della controparte, nel caso sia attivo un monitor senza procedenza di quest'ultimo
				flag_precedenza_gf= true	   // (siccome ci deve essere sempre un monitor con precedenza)

			ff_mod= true	// abilito il flag di ff_mod per tener traccia del fatto che è stato il piano ff a richiedere un adattamento
		}	
                      
		sendCommand(start_p_fd, ON)      // attivo componente P  
    }
  	else{
	    if (gf_call && flag_precedenza_gf) {                      // verifico quale dei 2 cicli ha la precedenza e vado ad abilitate le variabili mod rispettive
			gf_call= false
			flag_precedenza_gf= false 
			if (ff_call && !flag_precedenza_ff)
				flag_precedenza_ff= true

    		sendCommand(start_m_gf, ON)
	  }
		else if (ff_call && flag_precedenza_ff) {
			ff_call= false
			flag_precedenza_ff= false
			if (gf_call && !flag_precedenza_gf)
				flag_precedenza_gf= true

    		sendCommand(start_m_ff, ON)
		}
    }

	sendCommand(start_a_fd, OFF) 
end 

rule "Plan_p_fd" 
when
	Item start_p_fd received command ON
then
	if (adaptation_required_gf) {       // in caso la variabile di adattamento gf sia attiva guarda in quale esatta stanza è richiesta 
		adaptation_required_gf= false   // una modifica, andandola a salvare sulla variabile di modifica rispettiva
       
       if (kiTemp >= 45 && GF_Kitchen_Fire_Alarm.state == OFF) {
            gf_kit= 1
        }
        if (batTemp >= 45 && GF_Bathroom_Fire_Alarm.state== OFF) {
            gf_bat= 1
        }
        if (lzOneTemp >= 45 && GF_Living_Fire_Alarm.state == OFF) {
            gf_liv1= 1
        }
        if (lzTwoTemp >= 45 && GF_Living_Fire_Alarm.state == OFF) {
            gf_liv2= 1
        }

		sendCommand(start_e_gf, ON) 
	}

	if (adaptation_required_ff) {       // in caso la variabile di adattamento ff sia attiva guarda in quale esatta stanza è richiesta 
		adaptation_required_ff= false   // una modifica, andandola a salvare sulla variabile di modifica rispettiva

        if (cbTemp >= 45 && FF_Children_Bedroom_Fire_Alarm.state== OFF) {
            ff_cb= 1
        }
        if (bTemp >= 45 && FF_Bedroom_Fire_Alarm.state== OFF) {
            ff_bed= 1
        }

		sendCommand(start_e_ff, ON)
	}

	sendCommand(start_p_fd, OFF)
end

rule "Exec_e_gf"
when
	Item start_e_gf received command ON
then
    if (gf_bat== 1) {
        gf_bat= 0
        sendCommand(GF_Bathroom_Fire_Alarm, ON)
    }
    if (gf_kit== 1) {
        gf_kit= 0
        sendCommand (GF_Kitchen_Fire_Alarm, ON)
    }
    if (gf_liv1== 1) {
        gf_liv1= 0
        sendCommand (GF_Living_Fire_Alarm, ON)
    }
    if (gf_liv2== 1) {
        gf_liv2 = 0
        sendCommand (GF_Living_Fire_Alarm, ON)
    }

	sendCommand(start_e_gf, OFF)

    try {
        lock_e.lock()
    }
    finally {
        if (gf_mod) {    // reset alla rispettiva componente M chiamante
            gf_mod= false
            sendCommand(start_m_gf, ON)
        }
        else if (ff_mod) {
            ff_mod= false
            sendCommand(start_m_ff, ON)
        }
        lock_e.unlock()
    }
end

rule "Exec_e_ff"
when
	Item start_e_ff received command ON
then                           // si va a controllare lo stato delle variabili di modifica, andando ad attuare eventuali modifiche
    if (ff_bed== 1) {
        ff_bed= 0
        sendCommand (FF_Bedroom_Fire_Alarm, ON)
    }
    if (ff_cb== 1) {
        ff_cb= 0
        sendCommand (FF_Children_Bedroom_Fire_Alarm, ON)
    }
    
	sendCommand(start_e_ff, OFF)

    try {
        lock_e.lock()
    }
    finally {
        if (gf_mod) {    // reset alla rispettiva componente M chiamante
            gf_mod= false
            sendCommand(start_m_gf, ON)
        }
        else if (ff_mod) {
            ff_mod= false
            sendCommand(start_m_ff, ON)
        }
        lock_e.unlock()
    }
end 